plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id "org.jetbrains.dokka" version "1.9.20"

}

logger.lifecycle("Using version ${version} for ${name}")

android {
    compileSdk 34
    namespace "com.eartho.one"

    defaultConfig {
        minSdkVersion 21
        targetSdk 34

        buildConfigField "String", "LIBRARY_NAME", "\"$project.rootProject.name\""
        buildConfigField "String", "VERSION_NAME", "\"${project.version}\""

        consumerProguardFiles '../proguard/proguard-eartho.pro','../proguard/proguard-gson.pro', '../proguard/proguard-okio.pro', '../proguard/proguard-jetpack.pro'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig = true
    }


    testOptions {
        unitTests {
            all {
                maxHeapSize = "1024m"
            }
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                '-Xexplicit-api=strict', // or '-Xexplicit-api=warning'
        ]
    }
    lint {
        abortOnError true
        htmlReport true
    }
}

ext {
    okhttpVersion = '4.12.0'
    powermockVersion = '2.0.9'
    coroutinesVersion = '1.6.2'
    biometricLibraryVersion = '1.1.0'
    credentialManagerVersion = "1.3.0"
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    implementation(platform("com.squareup.okhttp3:okhttp-bom:$okhttpVersion"))
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.androidbrowserhelper:androidbrowserhelper:2.5.0'

    implementation "androidx.biometric:biometric:$biometricLibraryVersion"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation "org.powermock:powermock-module-junit4:$powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powermockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powermockVersion"
    testImplementation 'org.mockito:mockito-core:5.0.0'
    // Mockito-Kotlin: See https://github.com/nhaarman/mockito-kotlin/wiki/Parameter-specified-as-non-null-is-null
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
    testImplementation "com.squareup.okhttp3:okhttp-tls:$okhttpVersion"
    testImplementation 'com.jayway.awaitility:awaitility:1.7.0'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    testImplementation "androidx.biometric:biometric:$biometricLibraryVersion"

    implementation "androidx.credentials:credentials-play-services-auth:$credentialManagerVersion"
    implementation "androidx.credentials:credentials:$credentialManagerVersion"
}

ext {
    PUBLISH_GROUP_ID = 'io.github.eartho-group'
    PUBLISH_VERSION = version
    PUBLISH_ARTIFACT_ID = 'one'
    PUBLISH_DESCRIPTION = 'Eartho One Android SDK'
    PUBLISH_URL = 'https://github.com/eartho-group/eartho-packages'
    PUBLISH_LICENSE_NAME = 'Mozilla Public License 2.0'
    PUBLISH_LICENSE_URL =
            'https://github.com/eartho-group/eartho-packages'
    PUBLISH_DEVELOPER_ID = 'eartho-group'
    PUBLISH_DEVELOPER_NAME = 'Eartho'
    PUBLISH_DEVELOPER_EMAIL = 'contact@eartho.wrold'
    PUBLISH_SCM_CONNECTION =
            'scm:git:github.com/eartho-group/one-client-kotlin.git'
    PUBLISH_SCM_DEVELOPER_CONNECTION =
            'scm:git@github.com:eartho-group/one-client-kotlin.git'
    PUBLISH_SCM_URL =
            'https://github.com/eartho-group/eartho-packages'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

afterEvaluate {
    generateMetadataFileForReleasePublication.dependsOn androidSourcesJar
}
